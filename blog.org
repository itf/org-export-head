#+OPTIONS:   broken-links:mark
#+OPTIONS: toc:nil num:3 H:4 ^:nil pri:t title:nil  html-style:nil html5-fancy:t
#+HTML_DOCTYPE: html5
#+HTML_HEAD: <link rel="stylesheet" type="text/css" href="org.css"/>
* README                                                           :noexport:
Run the code inside the code block.

Run M-x org-export-head or (org-export-head directory backend)

The MENU and FOOTNOTES have to be :noexport: It cannot contain a property drawer at the moment.

* Code                                                             :noexport: 
#+BEGIN_SRC emacs-lisp   

;; based on http://pragmaticemacs.com/emacs/export-org-mode-headlines-to-separate-files/
;; export headlines to separate files
;; http://emacs.stackexchange.com/questions/2259/how-to-export-top-level-headings-of-org-mode-buffer-to-separate-files

(defun org-export-head--run-on-temp-copy-buffer (function-to-run &rest args)
  "Runs function on a temp buffer with the contents of the original buffer"
  (save-excursion
    (let ((temp-buffer (generate-new-buffer "tmp")))
      (copy-to-buffer temp-buffer (point-min) (point-max)) 
      (with-current-buffer temp-buffer 
        (org-mode) 
        (outline-show-all) 
        (apply function-to-run args))
      (kill-buffer temp-buffer))))

(defun org-export-head (&optional directory-name backend reexport)
  "Updates the hashes and reexport all changed headings if reexport is nil.
Reexports all headings if reexport is non-nil"
  (interactive)
  (let ((directory-name (or directory-name (read-directory-name "Directory:")))
        (backend (or backend "html")))
    (make-directory directory-name t)
    (org-export-head--run-on-temp-copy-buffer #'org-export-head--modify-buffer-ast directory-name backend reexport)
    (org-export-head--update-hashes)))


(defun org-export-head-reexport (&optional directory-name backend)
  "Reexports all the headings"
  (interactive)
  (org-export-head directory-name backend t))

(defun org-export-head--modify-buffer-ast (directory-path backend reexport)
  "Export all subtrees that are *not* tagged with :noexport: to
separate files.

Subtrees that do not have the :EXPORT_FILE_NAME: property set
are exported to a filename derived from the headline text."
  ;; Delete content that has already been exported and set it to noreexport
  (org-export-head--update-hashes)
  (if (not reexport)
      (org-export-head--delete-noreexport))
  
  ;;Get the parser tree and the headlines that will become files
  (let*  ((ast (org-element-parse-buffer))
          (headlines-hash-list (org-export-head--get-headlines-ast ast))
          (headlines-hash (car headlines-hash-list))
          (headlines-list (cdr headlines-hash-list))
          (headlines-hash (org-export-head--insert-next-previous-headline headlines-hash headlines-list)))
    
    
    ;;Get the templates
    (let ((header (org-export-head--get-content-subtree-match-tag-ast "header" ast))
          (index (org-export-head--get-index headlines-hash)))
      
      ;;For each not noexport headline apply the template, i.e. copy contents
      (org-export-head--run-on-match-headline-ast 
       #'(lambda (headline) 
           (org-export-head--insert-on-headline-footer-ast headline index))
       '("index") ast)
      
      (org-export-head--run-on-not-match-headline-ast 
       #'(lambda (headline) 
           (org-export-head--insert-on-headline-header-ast headline header))
       '("noexport" "noheader" "noreexport") ast)


      
      ;;Fix links -- order is important. First external than fuzzy links
      (org-element-map ast 'link
        (lambda (link)
          (let* ((link (or (org-export-head--fix-file-external-link-ast directory-path link) link))
                 (link (or (org-export-head--fix-local-link-ast headlines-hash link) link)))))))
    
    ;;Convert the buffer to contain the new AST, 
    ;;this is needed because the exporter expects the content to be in a buffer
    (erase-buffer) 
    (insert (org-element-interpret-data ast))

      ;;After applying the template we replace the macros on all places
      (org-export-head--run-on-each-heading 
       #'(lambda ()
           (let ((macro-plist (org-export-head--generate-macro-plist headlines-hash)))
             (org-export-head--replace-headline-macros macro-plist)))
       "-noexport-noreexport")

    (outline-show-all)
    
    ;;Finally export all the headers
    (org-export-head-export-headers directory-path backend)))



;;Not everything can be done using the AST, sadly.
;;Org element has no support for adding custom properties to headlines
;;Nor does it have a nice interface to grab the contents without the property drawer
;;Ideally everything would be done using the AST and org-element

;;START OF NON AST (non org-element) SESSION
(defun org-export-head--run-on-each-heading(fn match  &rest args)
  "Puts the point on each heading and runs the function. Needed for exporting all headings
   from  http://pragmaticemacs.com/emacs/export-org-mode-headlines-to-separate-files/"
  (save-excursion
    (goto-char (point-min))
    (goto-char (re-search-forward "^*"))
    (set-mark (line-beginning-position))
    (goto-char (point-max))
    (org-map-entries
     (lambda ()
       (apply fn args))
     match 'region-start-level)
    (deactivate-mark)))

(defun org-export-head-export-headers (directory-name backend)
  "Exports each heading to directory-name using backend"
  (if (equal backend "html")
      (org-export-head--run-on-each-heading 
       #'(lambda ()
           (org-set-property
            "EXPORT_FILE_NAME"
            (concat directory-name (org-export-head--escaped-headline)))
           (deactivate-mark)
           (org-html-export-to-html nil t)
           (set-buffer-modified-p t)) "-noexport-noreexport"))
  (if (equal backend "pdf")
      (org-export-head--run-on-each-heading 
       #'(lambda ()
           (org-set-property
            "EXPORT_FILE_NAME"
            (concat directory-name (org-export-head--escaped-headline)))
           (deactivate-mark)
           (org-latex-export-to-pdf nil t)
           (set-buffer-modified-p t)) "-noexport-noreexport")))

(defun org-export-head--goto-header(&optional no-new-line)
  "Puts point after property-block if it exists, in an empty line
  by creating a new line, unless no-new-line is non nil and returns point"
  (interactive)
  (org-back-to-heading t)
  (let* ((beg-end (org-get-property-block))
         (end (cdr beg-end)))
    (goto-char (or end (point))))
  (goto-char (point-at-bol 2)) ;;Advance one line
  (if (not no-new-line) 
      (progn
        (newline)
        (goto-char (point-at-bol 0)))) ;;Go back one line
  (point))

(defun org-export-head--get-content-subtree-at-point()
  "Gets the content of the subtree at point"
  (save-excursion
    (deactivate-mark t)
    (let ((start (org-export-head--goto-header t))
          (end (org-end-of-subtree t))) 
      (buffer-substring start end))))


;;; HASH code
;;Idea from https://emacs.stackexchange.com/a/39376/20165
(defun org-export-head--update-hashes()
  "Updates the hashes of all the headings"
  (org-export-head--run-on-each-heading 
   #'(lambda()
       (let ((new-hash  (format "%s" (org-export-head-get-hash-value-content)))
             (old-hash (org-entry-get-with-inheritance "HASH"))
             (older-hash (org-entry-get-with-inheritance "PREVIOUS-HASH"))) 
         (if (not old-hash)
             (progn
               (org-set-property "CREATION-DATE" (format-time-string "%Y-%m-%d"))))
         ;;If there was a change made
         (if (not (equal new-hash old-hash))
             (progn
               (org-set-property "MODIFICATION-DATE" (format-time-string "%Y-%m-%d"))
               (org-set-property "HASH" new-hash)))
         ;;Setting property is expensive
         (if (not (equal old-hash older-hash))
               (org-set-property "PREVIOUS-HASH" (or old-hash "")))))
   "-noexport"))


(defun org-export-head-get-hash-value-content()
  "Gets the hash of the subtree at point"
  (org-export-head-hash-function (org-export-head--get-content-subtree-at-point)))

(defun org-export-head-hash-function(text)
  (md5 text))

;;;END HASH CODE

(defun org-export-head--delete-noreexport()
  "Faster export by deleting things that won't be exported so we don't process them and their links"
  (org-export-head--run-on-each-heading 
   #'(lambda()
       (let ((old-hash (org-entry-get-with-inheritance "PREVIOUS-HASH"))
             (new-hash (org-entry-get-with-inheritance "HASH")))    
         ;;If there was a change made
         (if (equal new-hash old-hash)
             (progn
               (org-toggle-tag "noreexport" 'on)
               ;;faster export by deleting noexport things before processing
               (org-export-head--erase-content-subtree))))) 
   "-noexport"))

(defun org-export-head--erase-content-subtree()
  (save-excursion
    (let ((start (org-export-head--goto-header t))
          (end (org-end-of-subtree))) 
      (delete-region start end))))



(defun org-export-head--get-headlines ()
  "Returns a tuple that contains a hashtable of headline name to Alist of headline properties
As well as a list of the headline names"
  (flet ((make-hash ()
                   (make-hash-table :test 'equal))
         (add-to-hash (hashtable)
                      (puthash (org-export-head--headline) (org-entry-properties) hashtable)))
    (let ((headlines-hash (make-hash))
          (headlines-list ()))
      (org-export-head--run-on-each-heading 
       #'(lambda()
           (add-to-hash headlines-hash)
           (setq headlines-list (cons (org-export-head--headline) headlines-list)))
       "-noexport -nosomething")
      (cons headlines-hash headlines-list))))


(defun org-export-head--headline ()
  "Gets the headline title if point is at the headline"
  (nth 4 (org-heading-components)))

(defun org-export-head--escaped-headline ()
  (org-export-head--escape (org-export-head--headline)))


(defun org-export-head--replace-headline-macros(macro-plist)
  (save-excursion
    (org-back-to-heading)
    ;;End of subtree might change because of macro expansion, so it is recalculated.
    (while (re-search-forward "\\#\\#\\#\\([-A-Za-z_]+\\)\\#\\#\\#" (save-excursion (org-end-of-subtree)) t)
      (unless (org-in-src-block-p)
        (let* ((macro (match-string-no-properties  1))
               (macro-subs (lax-plist-get macro-plist macro)))
          (if macro-subs
              (replace-match macro-subs)
            (replace-match "")))))))

(defun org-export-head--generate-macro-plist(headlines-hash)
  (let* ((headline-name (org-export-head--headline))
         (headline (gethash headline-name headlines-hash nil))
         (macro-plist ())
         (macro-plist (lax-plist-put macro-plist "next" (org-element-property :NEXT headline)))
         (macro-plist (lax-plist-put macro-plist "previous" (org-element-property :PREVIOUS headline)))
         (macro-plist (lax-plist-put macro-plist "next-url" (org-element-property :NEXT_URL headline)))
         (macro-plist (lax-plist-put macro-plist "previous-url" (org-element-property :PREVIOUS_URL headline))))
    macro-plist))
    

;;END OF NON AST (non org-element) SESSION


(defun org-export-head--fix-local-link-ast (headlines link)
  "Fixes fuzzy links to headlines, so the they point to new files"
  (flet ((get-hash (element set)
                   (gethash element set nil)))
    (when (string= (org-element-property :type link) "fuzzy")
      (let* ((path  (org-element-property :path link))
             (new-path (get-hash path headlines))) 
        (when new-path
          (let ((link-copy (org-element-copy link)))
            (apply #'org-element-adopt-elements link-copy (org-element-contents link))
            (org-element-put-property link-copy :type "file")
            (org-element-put-property link-copy :path (concat (org-export-head--escape path) ".org"))
            (org-element-set-element link link-copy)))))))


(defun org-export-head--fix-file-external-link-ast (directory-path link)
  "Creates hard links to the external files in the output directory"
  (when (string= (org-element-property :type link) "file")
    (let* ((path (org-element-property :path link))
           (link-copy (org-element-copy link))
           ;;Removes ../ from the releative path of the file to force it to be moved to a subfolder
           ;;of the current dir. This causes some file conflits in edge cases
           ;;e.g: ../images and ../../images will map to the same place. This should be rare in normal usage
           (new-relative-path 
            (replace-regexp-in-string "\\.\\./" "" (file-relative-name path)))
           (new-hard-link-path (concat directory-path new-relative-path))
           (new-hard-link-directory (file-name-directory new-hard-link-path)))
      
      ;;Fix the AST
      (apply #'org-element-adopt-elements link-copy (org-element-contents link))
      (org-element-put-property link-copy :path new-relative-path)
      (org-element-set-element link  link-copy)
      
      ;;Create hard link folder
      (make-directory new-hard-link-directory t)
      ;;Create hard link, not replacing if it already exists, catching error if file does not exist
      (condition-case nil
          (add-name-to-file path new-hard-link-path nil)
        (error nil)))))


;;This is a hack. Creates index in a separate buffer headline
;;Then copy the contents of the headline; 
;;Ideally it should be done by using org-element-create; but this didnt' work.
(defun org-export-head--get-index (headlines)
  "Generates a list of all the exported pages"
  (save-excursion
    (let ((temp-buffer (generate-new-buffer "temp"))
          (contents nil))
      (with-current-buffer temp-buffer 
        (org-mode) 
        (outline-show-all) 
        (insert "* Pages\n\n") ;; Ignored. Necessary for org-element-contents to work properly.
        (maphash (lambda(key value) (insert (concat "- [[" key "][" key "]]\n")))  headlines)
        (setq contents(org-element-contents (car (org-element-contents (org-element-parse-buffer))))))
      (kill-buffer temp-buffer)
      contents)))

(defun org-export-head--run-on-not-match-headline-ast (fn not-match ast)
  "Runs the function on headlines that don't match 'not-match' "
  (org-element-map ast 'headline
    (lambda (headline)
      (unless (cl-intersection not-match (org-element-property :tags headline) :test 'equal) 
        (when (equal 1 (org-element-property :level headline))
          (funcall fn headline))))))


(defun org-export-head--run-on-match-headline-ast (fn match ast)
  "Runs the function on headlines that match 'match' "
  (org-element-map ast 'headline
    (lambda (headline)
      (when (cl-intersection match (org-element-property :tags headline) :test 'equal)
        (when (equal 1 (org-element-property :level headline))
          (funcall fn headline))))))


(defun org-export-head--get-headlines-ast(ast)
  (flet ((make-hash ()
                    (make-hash-table :test 'equal))
         (add-to-hash (headline set)
                      (puthash (org-element-property :raw-value headline)  headline set)))
    (let ((headlines-hash (make-hash))
          (headlines-list '()))
      (org-export-head--run-on-not-match-headline-ast
       #'(lambda(headline) 
           (add-to-hash headline headlines-hash)
           (setq headlines-list (cons (org-element-property :raw-value headline)  headlines-list)))
       '("noexsport") ast)
      (cons headlines-hash headlines-list))))



(defun org-export-head--insert-next-previous-headline(headlines-hash headlines-list)
  (let* ((temp-list (cons nil headlines-list))
        (len (length headlines-list)))
    (dotimes (i len)
      (let* ((previous (nth 0 temp-list))
            (headline-name (nth 1 temp-list))
            (next (nth 2 temp-list))
            (headline (gethash headline-name headlines-hash nil)))
            (org-element-put-property headline :PREVIOUS previous)
            (org-element-put-property headline :NEXT next)
            (org-element-put-property headline :PREVIOUS_URL (concat "file:" (org-export-head--headline-to-file previous)))
            (org-element-put-property headline :NEXT_URL (concat "file:" (org-export-head--headline-to-file next))))
        (setq temp-list (cdr temp-list))))
  headlines-hash)
      

(defun org-export-head--headline-to-file(headline-name)
  "Generate the file name of the headline"
  (concat (org-export-head--escape headline-name) ".org"))


;;Quite ugly, but necessary to not insert things inside the properties drawer
(defun org-export-head--insert-on-headline-header-ast(headline contents)
  "Inserts contents after the properties of headline."
  ;;The only way to find where the properties end is by scanning the tree and knowing
  ;;That any content inside a headline is inside a "section" element.
  (let* ((headline-contents (org-element-contents headline))
         (section-contents  (org-element-contents (car headline-contents)))
         (next-child (car (cdr headline-contents)))
         (child (car section-contents)))
    (while (memq (org-element-type child) '(planning drawer property-drawer))
      (setq section-contents (cdr section-contents))
      (setq child (car section-contents)))
    (setq child (or child next-child))
    (if child 
        (dolist (element contents) 
          (org-element-insert-before element child))
      (apply #'org-element-adopt-elements headline contents))))


(defun org-export-head--insert-on-headline-footer-ast (headline contents)
  "Inserts contents at the end of the subtree"
  (apply #'org-element-adopt-elements headline contents))

(defun org-export-head--escape(text)
  (when text
    (let* ((text (replace-regexp-in-string " " "_" text))
           (text (replace-regexp-in-string "/" "-" text))
           (text (replace-regexp-in-string "[\\?.,!]" "" text)))
      text)))

(defun org-export-head--get-content-subtree-match-tag-ast(match ast)
  (let ((contents nil))
    (org-element-map ast 'headline
      (lambda (headline)
        (when (member match (org-element-property :tags headline))
          (setq contents   (nconc contents (org-element-contents headline)))))) 
    contents))



;;Functions not in use that can be used to easily add more template things.
(defun org-export-head--insert-content-match-headline-in-not-match-headline-ast(from-match to-not-match headline)
  (org-export-head--run-on-not-match-headline-ast 
   #'(lambda (headline) 
       (org-export-head--insert-on-headline-header-ast 
        headline
        (org-export-head--get-content-subtree-match-tag-ast from-match ast)))
   to-not-match ast))


(defun org-export-head--insert-content-match-headline-in-match-headline-ast(from-match to-match headline)
  (org-export-head--run-on-not-match-headline-ast 
   #'(lambda (headline) 
       (org-export-head--insert-on-headline-header-ast 
        headline
        (org-export-head--get-content-subtree-match-tag-ast from-match ast)))
   to-match ast))



#+END_SRC

#+RESULTS:
: org-export-head--insert-content-match-headline-in-match-headline-ast


* Includes :noexport:
Creates a hard link to org.css in the export directory.
[[file:./org.css]]

* Menu :noexport:header:
#+begin_head
#+begin_title
[[Index][Ivan @ Home]]
#+end_title
#+begin_catch-phrase
Excelsior!
#+end_catch-phrase

#+begin_nav
- [[Index][home]]
- [[Foo][Foo!!]]
- [[Foo Bar][Foo Bar!!]]
- [[Broken][Should say Broken link]]
#+end_nav
#+end_head


@@html: <h1>@@
{{{title}}} @@comment: This is the title of the headline @@
@@html: </h1>@@

#+TOC: headlines 2

* Index :index:
This is my index! Sadly during export I lose access to all information related to it. 

Anyway:
** Index is here!



   
* Foo
** subheader 0
SCHEDULED: <2018-08-31 Fri>

This is an example page that will be generated. 


** subheader1
** Subheader 2
*** Subheader 2.1
**** Subheader 2.1.1
** Subheader 3

   [[Subheader 2.1.1][Internal link!]]

   [[Bar][External Link without space!]]

   [[Foo Bar][External Link with space!]]




#+BEGIN_SRC emacs-lisp 
[[test]] ;Show that links in code are not affected!
#+END_SRC

* Bar
  
Here we show that footnotes work [fn:1].

They are pretty great! [fn:2]

* Foo Bar
  Nothing to see here
(org-export-head)
  ###previous###
* Footnotes :noexport:

[fn:2] My second foot note!

[fn:1] How does this work


